#!/usr/bin/perl -wT

##############################################################################
# PURPOSE: tidalserver - Serve a NOAA tidal station data, subsetted by
#          longitude-latitude box, in ASCII format for use by
#          EstuaryDataMapper (via wget).
#
# NOTES:   Server is WCS-compliant:
#          https://www.opengeospatial.org/standards/wcs
#          https://datafed.net/xs/OpenGIS/wcs/1.0.0/
#          and modeled after a similar webserver application:
#          https://webapps.datafed.net/ogc_EPA.wsfl
#
#          Connects to external web service:
#          https://tidesandcurrents.noaa.gov/api/datagetter?
#
#          Example usage:
#
#          wget -q -T 0 -O - 'https://ofmpub.epa.gov/rsig/tidalserver?\
#                     SERVICE=wcs&\
#                     VERSION=1.0.0&\
#                     REQUEST=GetCoverage&\
#                     COVERAGE=mtl&\
#                     TIME=2006-08-26T00:00:00Z/2006-08-27T23:59:59Z&\
#                     BBOX=-89,30,-88,31&\
#                     FORMAT=ascii'
#
# HISTORY: 2012/06/28, plessel.todd@epa.gov, 1-919-541-5500
#
# STATUS:  unreviewed, tested.
##############################################################################


use strict;
package main;
$| = 1; # Turn off output buffering so messages appear in correct order.
alarm( 3600 ); # Kill process after 1 hour. Legitimate usage could be an hour!
# Restrict PATH and ENV.
delete @ENV{ 'PATH', 'IFS', 'CDPATH', 'ENV', 'BASH_ENV' };


############################## TUNABLE CONSTANTS #############################


my $debugging = 0; # 1 = print debug messages to STDERR logs/error_log.

# Server where this program is installed:

my $server_path = 'https://YOUR_WEBSERVER_HOST/cgi-bin';

# Stations file contains static info on external web service stations:

my $stations_file_name = '/data/land_use/tidal_stations.txt';

# External server to forward to via wget command:

my $bindir = '/code/bin/Linux.x86_64';
#my $wget = "$bindir/wget -q -t 1 -T 0 -O -";
my $wget = '/usr/bin/curl -k --silent --max-time 3600 --retry 0 -L --tcp-nodelay';

my $wget_command =
  "$wget " . "'" .
  'https://tidesandcurrents.noaa.gov/api/datagetter?' .
  'product=water_level&application=NOS.COOPS.TAC.WL&format=csv&' .
  'units=metric&time_zone=gmt';

# Query string parsing routine dispatch table:

my %parsers = (
  'service'  => \&parse_service_option,
  'version'  => \&parse_version_option,
  'request'  => \&parse_request_option,
  'coverage' => \&parse_coverage_option,
  'time'     => \&parse_time_option,
  'bbox'     => \&parse_bbox_option,
  'format'   => \&parse_format_option
);

# Webserver content types for each output format:

my %content_types = (
  'ascii'         => 'text/plain',
  'bin'           => 'application/octet-stream',
  'xdr'           => 'application/octet-stream',
  'netcdf-coards' => 'application/netcdf',
  'netcdf-ioapi'  => 'application/netcdf'
);

# Full URL path to this program:

my @pathed_program_parts = split( /\//, $0 );
my $program_name = $pathed_program_parts[ @pathed_program_parts - 1 ];
my $program = "$server_path/$program_name";

# Metadata:
# name - this is the COVERAGE= name used by this script.
#        '?' means currently unimplmented by the remote web service.
# units - units for the parameter, e.g., mg/L.
# description - Parameter description.
# parameters - Comma-separated list of parameter names used by the called
#              remote web service:
# https://tidesandcurrents.noaa.gov/api/datagetter?

my @metadata = (
  {
    'name' => 'station',
    'units' => '',
    'description' => 'Station-ID.',
    'parameters' => ''
  },
  {
    'name' => 'mhhw',
    'units' => 'm',
    'description' => 'Mean Higher High Water. ' .
      'The average of the higher high water height of each tidal day ' .
      'observed over the National Tidal Datum Epoch',
    'parameters' => 'mhhw'
  },
  {
    'name' => 'mhw',
    'units' => 'm',
    'description' => 'Mean High Water. ' .
      'The average of all the high water heights observed over the ' .
      'National Tidal Datum Epoch.',
    'parameters' => 'mhw'
  },
  {
    'name' => 'mtl',
    'units' => 'm',
    'description' => 'Mean Tide Level. ' .
      'The arithmetic mean of mean high water and mean low water.',
    'parameters' => 'mtl'
  },
  {
    'name' => 'msl',
    'units' => 'm',
    'description' => 'Mean Sea Level. ' .
      'The arithmetic mean of hourly heights observed over the ' .
      'National Tidal Datum Epoch.',
    'parameters' => 'msl'
  },
  {
    'name' => 'mlw',
    'units' => 'm',
    'description' => 'Mean Low Water. ' .
      'The average of all the low water heights observed over the ' .
      'National Tidal Datum Epoch.',
    'parameters' => 'mlw'
  },
  {
    'name' => 'mllw',
    'units' => 'm',
    'description' => 'Mean Lower Low Water. ' .
      'The average of the lower low water height of each tidal day ' .
      'observed over the National Tidal Datum Epoch.',
    'parameters' => 'mllw'
  }
);


################################## VARIABLES #################################


# Parsed from the URL query string:

my $service  = ''; # wcs.
my $version  = ''; # 1.0.0.
my $request  = ''; # getcapabilities or describecoverage or getcoverage.
my $coverage = ''; # Saved until time has been parsed.
my $variable = ''; # o3, ...
my $format   = ''; # ascii.
my $time     = ''; # E.g., 2001-08-29t00:00:00z/2001-08-31t23:59:59z.
my $bbox     = ''; # E.g., -90,28,-80,32,1,1. Where 1,1 means layer 1 only.

# Derived from the above parsed values:

my @bounds = split( /,/, '-180,-90,180,90' );
my $starting_timestamp = 0;  # yyyymmddhh, e.g., 2005082600.
my $ending_timestamp   = 0;  # yyyymmddhh, e.g., 2005082600.
my $hours              = 0;  # E.g., 5 days = 5 x 24 = 120.
my $days               = 0;  # E.g., 5.
my $ending_yyyymmdd    = 0;  # yyyymmdd, e.g., 20050826.
my @stations_info = ();
my $coverage_units = '';
my $remote_queries = 0; # Number of calls to remote web service.

################################## ROUTINES ##################################


main();


sub main {
  my $result = 0;

  if ( ! parse_url() ) {
    print "Content-type: text/plain; charset=iso-8859-1\n\n"; # For webserver
  } elsif ( $request eq 'getcapabilities' ) {
    print "Content-type: text/xml; charset=iso-8859-1\n\n"; # For webserver
    print_capabilities();
    $result = 1;
  } elsif ( $request eq 'describecoverage' ) {
    print "Content-type: text/xml; charset=iso-8859-1\n\n"; # For webserver
    print_coverage_description();
    $result = 1;
  } elsif ( $request eq 'getcoverage' ) {
    compute_time_range();
    print "Content-type: $content_types{ $format }; charset=iso-8859-1\n\n";
    open( FH, "< $stations_file_name" ) or
      die "Can't open $stations_file_name for read: $!";
    @stations_info = <FH>;
    close FH or die "Cannot close $stations_file_name: $!";

    if ( $coverage eq 'station' ) {
      my @stations = get_stations(); # Get stations inside bbox.
      my $count = @stations;
      print "$count\n";

      if ( $count ) { # Print output: count of data points, header, data lines:
        $result = 1;
        print "start(yyyymmdd)\tend(yyyymmdd)\tlongitude(deg)\tlatitude(deg)" .
              "\tstation_id(-)\n";

        for ( my $station = 0; $station < $count; ++$station ) {
          print "$stations[ $station ]\n";
        }
      }
    } else {
      my @output = do_queries(); # Query remote web service many times...
      my $count = @output;
      debug( "$remote_queries calls to remote web service yielded " .
             "$count values." );
      print "$count\n";

      if ( $count ) { # Print output: count of data points, header, data lines:
        $result = 1;
        print "timestamp(UTC)\tlongitude(deg)\tlatitude(deg)\tstation_id(-)\t";
        print "$coverage($coverage_units)\n";

        for ( my $i = 0; $i < $count; ++$i ) {
          print "$output[ $i ]\n";
        }
      }
    }
  }

  $result = 1 - $result; # UNIX: zero is success, non-zero is failure.
  debug( "exit result = $result" );
  exit $result;
}



# Print web server capabilities metadata.

sub print_capabilities {
  print '<?xml version="1.0" encoding="UTF-8" ?>' . "\n";
  print '<WCS_Capabilities version="1.0.0" ';
  print 'xmlns="https://www.opengeospatial.org/standards/wcs" ';
  print 'xmlns:gml="https://www.opengis.net//gml" ';
  print 'xmlns:xlink="https://www.w3.org/1999/xlink">';
  print '
    <Service>
        <metadataLink xlink:type="simple" xlink:href=" https://www.epa.gov/rsig" metadataType="other" />
        <description>EPA Tidal Web Server 1.0.0</description>
        <name>EPA_Tidal_OGC_WCS_1.0.0</name>
        <label>EPA Tidal Web Server 1.0.0</label>
        <keywords>
            <keyword>EPA</keyword>
            <keyword>Tidal</keyword>
            <keyword>interoperability</keyword>
        </keywords>
        <responsibleParty>
            <individualName>Todd Plessel</individualName>
            <organisationName>EPA Vislab</organisationName>
            <contactInfo>
                <onlineResource xlink:type="simple" xlink:href="mailto:plessel.todd@epa.gov" />
            </contactInfo>
        </responsibleParty>
        <fees>NONE</fees>
        <accessConstraints>NONE</accessConstraints>
    </Service>
    <Capability>
        <Request>
            <GetCapabilities>
                <DCPType>
                    <HTTP>
                        <Get>
                            <OnlineResource xlink:type="simple" xlink:href="https://ofmpub.epa.gov/rsig/tidalserver?" />
                        </Get>
                        <Post>
                            <OnlineResource xlink:type="simple" xlink:href="https://ofmpub.epa.gov/rsig/tidalserver" />
                        </Post>
                    </HTTP>
                </DCPType>
            </GetCapabilities>
            <DescribeCoverage>
                <DCPType>
                    <HTTP>
                        <Get>
                            <OnlineResource xlink:type="simple" xlink:href="https://ofmpub.epa.gov/rsig/tidalserver?" />
                        </Get>
                        <Post>
                            <OnlineResource xlink:type="simple" xlink:href="https://ofmpub.epa.gov/rsig/tidalserver" />
                        </Post>
                    </HTTP>
                </DCPType>
            </DescribeCoverage>
            <GetCoverage>
                <DCPType>
                    <HTTP>
                        <Get>
                            <OnlineResource xlink:type="simple" xlink:href="https://ofmpub.epa.gov/rsig/tidalserver?" />
                        </Get>
                        <Post>
                            <OnlineResource xlink:type="simple" xlink:href="https://ofmpub.epa.gov/rsig/tidalserver" />
                        </Post>
                    </HTTP>
                </DCPType>
            </GetCoverage>
        </Request>
        <Exception>
            <Format>text/xml; charset="utf-8"</Format>
        </Exception>
    </Capability>
    <ContentMetadata version="1.0.0">';
  print "\n";

  my $variable_count = @metadata;

  for ( my $variable = 0; $variable < $variable_count; ++$variable ) {
    my $name = $metadata[ $variable ]->{ name };

    if ( $name ne '?' ) {
      my $units = $metadata[ $variable ]->{ units };
      my $description = $metadata[ $variable ]->{ description };
      print "        <CoverageOfferingBrief>\n";
      print "            <name>$name</name>\n";
      print "            <label>$name($units)</label>\n";
      print "            <description>$description</description>\n";
      print '            <lonLatEnvelope srsName="WGS84(DD)">';
      print "\n";
      print "                <gml:pos>-126 22</gml:pos>\n";
      print "                <gml:pos>-65 50</gml:pos>\n";
      print "            </lonLatEnvelope>\n";
      print "        </CoverageOfferingBrief>\n";
    }
  }

  print "    </ContentMetadata>\n";
  print "</WCS_Capabilities>\n";
}



# Print web server coverage description metadata.

sub print_coverage_description {
  print '<?xml version="1.0" encoding="UTF-8" ?>';
  print '<CoverageDescription version="1.0.0" ';
  print 'xmlns="https://www.opengeospatial.org/standards/wcs" ';
  print 'xmlns:gml="https://www.opengis.net/gml" ';
  print 'xmlns:xlink="https://www.w3.org/1999/xlink">';
  print "\n";

  my $variable_count = @metadata;

  for ( my $variable = 0; $variable < $variable_count; ++$variable ) {
    my $name = $metadata[ $variable ]->{ name };

    if ( $name ne '?' ) {
      my $units = $metadata[ $variable ]->{ units };
      my $description = $metadata[ $variable ]->{ description };

      if ( $coverage eq '' || $coverage eq $name ) {
        print "    <CoverageOffering>\n";
        print "        <name>$name</name>\n";
        print "        <label>$name($units)</label>\n";
        print "        <description>$description</description>\n";
        print "        <domainSet>\n";
        print "            <spatialDomain>\n";
        print '                <gml:Envelope srsName="WGS84(DD)">';
        print "\n";
        print "                    <gml:pos>-126 22</gml:pos>\n";
        print "                    <gml:pos>-65 50</gml:pos>\n";
        print "                </gml:Envelope>\n";
        print "            </spatialDomain>\n";
        print "            <temporalDomain>\n";
        print "                <timePeriod>\n";
        print "                    <beginPosition>19940501";
        print "T00:00:00Z</beginPosition>\n";
        print "                    <endPosition>20151213";
        print "T23:59:59Z</endPosition>\n";
        print "                    <timeResolution>PT1Y</timeResolution>\n";
        print "                </timePeriod>\n";
        print "            </temporalDomain>\n";
        print "        </domainSet>\n";
        print "        <rangeSet>\n";
        print "            <RangeSet>\n";
        print "                <name>$name</name>\n";
        print "                <label>$name($units)</label>\n";
        print "                <description>$description</description>";
        print '
                <nullValues>
                    <singleValue>-99</singleValue>
                </nullValues>
            </RangeSet>
        </rangeSet>
        <supportedCRSs>
            <requestResponseCRSs>CRS:84</requestResponseCRSs>
            <nativeCRSs>CRS:84</nativeCRSs>
        </supportedCRSs>
        <supportedFormats>
            <formats>ASCII</formats>
        </supportedFormats>
        <supportedInterpolations>
            <interpolationMethod>mean mode</interpolationMethod>
        </supportedInterpolations>
    </CoverageOffering>
';

      }
    }
  }

  print "</CoverageDescription>\n";
}



# Parse URL parameters into variables.

sub parse_url {
  use CGI qw/ untaint /;     # Load Safe untainting CGI routines.
  $CGI::DISABLE_UPLOADS = 1; # Disable uploads.
  $CGI::POST_MAX = 1024;     # Limit posts to 1024 bytes.
  my $query = new CGI;       # Parse QUERY_STRING.
  %ENV = ();                 # Unset all environment variables.
  my @names = $query->param; # Extract names of parameters.
  my $count = @names;
  my $result = 0;
  debug( "$program_name @names ($count)" );

  for ( my $parameter = 0; $parameter < $count; ++$parameter) {
    $_ = $names[ $parameter ];                # $_ is tainted.
    s/[^A-Za-z]/_/go;                         # Untaint $_.
    my $option = $_;                          # $option is untainted.
    $_ = $query->param( $option );            # $_ is tainted.
    s/[^\w\-.,:\/]/_/go;                      # Untaint $_.
    my $value = $_;                           # $value is untainted.
    my $lowercase_option = lc( $option );
    my $lowercase_value  = lc( $value );
    debug( "$lowercase_option $lowercase_value" );

    if ( $parsers{ $lowercase_option } ) {
      $result = $parsers{ $lowercase_option }->( $lowercase_value );
    } else {
      print STDERR "\nInvalid option: '$option'\n";
      $result = 0;
    }

    if ( ! $result ) {
      $parameter = $count; # Finish looping.
    }
  }

  $result = $result && required_options_specified();
  return $result;
}



# Parse service option.

sub parse_service_option {
  my $value = shift;
  my $result = parse_option( $service, $value, 'SERVICE', 'wcs' );
  return $result;
}



# Parse version option.

sub parse_version_option {
  my $value = shift;
  my $result = parse_option( $version, $value, 'VERSION', '1.0.0' );
  return $result;
}



# Parse request option.

sub parse_request_option {
  my $value = shift;
  my $result = parse_option( $request, $value, 'REQUEST',
                             'getcoverage getcapabilities describecoverage' );
  return $result;
}



# Parse format option:

sub parse_format_option {
  my $value = shift;
  my $result =
    parse_option( $format, $value, 'FORMAT', 'ascii' );
  return $result;
}



# Parse coverage option.

sub parse_coverage_option {
  my $value = shift;
  my $result = 0;

  if ( $coverage ne '' ) {
    print STDERR "\nRedundant COVERAGE option: '$value'\n";
  } else {
    my $variable_count = @metadata;

    for ( my $variable = 0; $coverage eq '' && $variable < $variable_count;
          ++$variable ) {
      my $name = $metadata[ $variable ]->{ name };

      if ( $name ne '?' ) {

        if ( $name eq $value ) {
          $coverage = $name;
          $coverage_units = $metadata[ $variable ]->{ units };
          $result = 1;
        }
      }
    }

    if ( ! $result ) {
      print STDERR "\nInvalid COVERAGE option: '$value'\n";
    }
  }

  return $result;
}



# Parse time option.

sub parse_time_option {
  my $value = shift;
  my $result = 0;

  if ( $time ne '' ) {
    print STDERR "\nRedundant TIME option: '$value'\n";
  } else {
    $time = $value;
    my $is_valid = is_valid_time( $time );

    if ( ! $is_valid ) {
      print STDERR "\nInvalid TIME option: '$value'\n";
    } else {
      $result = 1;
    }
  }

  return $result;
}



# Parse bbox option.

sub parse_bbox_option {
  my $value = shift;
  my $result = 0;

  if ( $bbox ne '' ) {
    print STDERR "\nRedundant BBOX option: '$value'\n";
  } else {
    $bbox = $value;
    @bounds = split( /,/, $bbox );
    my $bounds_count = @bounds;

    if ( $bounds_count != 4 && $bounds_count != 6 ) {
      print STDERR "\nInvalid bbox option: '$value'\n";
    } elsif ( ! in_range( $bounds[ 0 ], -180.0, 180.0 ) ) {
      print STDERR "\nInvalid bbox option: '$bounds[ 0 ]'\n";
    } elsif ( ! in_range( $bounds[ 1 ], -90.0, 90.0 ) ) {
      print STDERR "\nInvalid bbox option: '$bounds[ 1 ]'\n";
    } elsif ( ! in_range( $bounds[ 2 ], $bounds[ 0 ], 180.0 ) ) {
      print STDERR "\nInvalid bbox option: '$bounds[ 2 ]'\n";
    } elsif ( ! in_range( $bounds[ 3 ], $bounds[ 1 ], 90.0 ) ) {
      print STDERR "\nInvalid bbox option: '$bounds[ 3 ]'\n";
    } else {
      $result = 1;
    }
  }

  return $result;
}


# Check that all required 'options' have been specified:

sub required_options_specified {
  my $result = 0;

  if ( $service eq '' ) {
    print STDERR "\nMissing option: 'SERVICE'\n";
  } elsif ( $version eq '' ) {
    print STDERR "\nMissing option: 'VERSION'\n";
  } elsif ( $request eq '' ) {
    print STDERR "\nMissing option: 'REQUEST'\n";
  } elsif ( $request eq 'getcoverage' ) {

    if ( $format eq '' ) {
      print STDERR "\nMissing option: 'FORMAT'\n";
    } elsif ( $time eq '' ) {
      print STDERR "\nMissing option: 'TIME'\n";
    } elsif ( $bbox eq '' ) {
      print STDERR "\nMissing option: 'BBOX'\n";
    } elsif ( $coverage eq '' ) {
      print STDERR "\nMissing option: 'COVERAGE'\n";
    } else {
      $result = 1;
    }
  } else {
    $result = 1;
  }

  return $result;
}



# Compute starting_timestamp and hours.
# inputs:  $time = '2001-08-26t00:00:00z/2001-08-31t23:59:59z'
# outputs: $starting_timestamp = 2005082600
#          $hours = 120
#          $days = 5

sub compute_time_range {
  my $yyyy1 = substr( $time, 0, 4 );
  my $mm1   = substr( $time, 5, 2 );
  my $dd1   = substr( $time, 8, 2 );
  my $hh1   = substr( $time, 11, 2 );
  my $i     = index( $time, '/' );

  $starting_timestamp = integer_timestamp( $yyyy1, $mm1, $dd1, $hh1 );
  $ending_yyyymmdd = $starting_timestamp / 100;
  $hours = 1;
  $days = 1;

  if ( $i != -1 ) {
    ++$i;
    my $yyyy2 = substr( $time, $i + 0, 4 );
    my $mm2   = substr( $time, $i + 5, 2 );
    my $dd2   = substr( $time, $i + 8, 2 );
    my $hh2   = substr( $time, $i + 11, 2 );
    my $yyyy  = $yyyy1;
    my $mm    = $mm1;
    my $dd    = $dd1;
    my $hh    = $hh1;
    my $previous_dd = $dd1;

    while ( integer_timestamp( $yyyy, $mm, $dd, $hh ) !=
            integer_timestamp( $yyyy2, $mm2, $dd2, $hh2 ) ) {
      increment_timestamp( $yyyy, $mm, $dd, $hh );

      if ( $dd ne $previous_dd ) {
        ++$days;
        $previous_dd = $dd;
        $ending_yyyymmdd = $yyyy * 10000 + $mm * 100 + $dd;
      }

      ++$hours;
    }
  }
}



# Get query parameters that correspond to $coverage:

sub get_parameters {
  my $result = '';
  my $variable_count = @metadata;

  for ( my $variable = 0; $result eq '' && $variable < $variable_count;
        ++$variable ) {
    my $name = $metadata[ $variable ]->{ name };

    if ( $coverage eq $name ) {
      $result = $metadata[ $variable ]->{ parameters };
    }
  }

  return $result;
}



# Get array of "longitude, latitude, station"
# strings that match $time and $bbox:

sub get_stations {
  my @result = ();
  my $count = @stations_info;
  my $yyyymmdd1 = substr( $starting_timestamp, 0, 8 );
  my $yyyymmdd2 = $ending_yyyymmdd;

  for ( my $s = 0; $s < $count; ++$s ) {
    my @line = split( /,/, $stations_info[ $s ] );
    my $state              = $line[ 0 ];
    my $station            = int( $line[ 1 ] ); # Remove any leading zeros.
    my $longitude          = $line[ 2 ];
    my $latitude           = $line[ 3 ];
    my $start_yyyymmdd     = $line[ 4 ];
    my $end_yyyymmdd       = $line[ 5 ];
    #my $huc                = $line[ 6 ];
    #my $description        = $line[ 7 ];
    #my $count = @line;

    #for ( my $d = 8; $d < $count; ++$d ) {
    #  $description .= ", $line[$d]";
    #}

    if ( $end_yyyymmdd == 0 ) {
      $end_yyyymmdd = 21001231;
    }

    $longitude =~ s/^\s+//; # Trim any leading spaces.
    $latitude  =~ s/^\s+//;

    if ( $longitude >= $bounds[0] && $longitude <= $bounds[2] &&
         $latitude  >= $bounds[1] && $latitude  <= $bounds[3] ) {

      if ( $coverage eq 'station' ) {
        push( @result,
          "$start_yyyymmdd\t$end_yyyymmdd\t$longitude\t$latitude\t$station" );
      } elsif ( ! ( $start_yyyymmdd > $yyyymmdd2 ||
                    $end_yyyymmdd   < $yyyymmdd1 ) ) {
        push( @result, "$longitude,$latitude,$station" );
      }
    }
  }

  return @result;
}



# Execute many queries to remote web service to obtain sorted array of
# timestamp longitude latitude station value
# for all stations in bbox reporting value within time-range.
# my @result = do_queries();

sub do_queries {
  my @result = ();
  my $parameter = get_parameters();
  my @stations = get_stations();
  my $count = @stations;
  my $yyyymmdd1 = substr( $starting_timestamp, 0, 8 );
  my $days_remaining = $days;
  debug( "Querying $count stations over $days days..." );

  while ( $days_remaining > 0 ) {
    my $query_days = $days_remaining < 31 ? $days_remaining : 31;
    my $yyyymmdd2 = increment_date_days( $yyyymmdd1, $query_days - 1 );

    for ( my $s = 0; $s < $count; ++$s ) {
      my @station_line = split( /,/, $stations[ $s ] );
      my $parts = @station_line;

      if ( $parts == 3 ) {
        my $longitude = $station_line[ 0 ];
        my $latitude  = $station_line[ 1 ];
        my $station   = $station_line[ 2 ];

        my @station_result =
          query_station( $longitude, $latitude, $station, $parameter,
                         $yyyymmdd1, $yyyymmdd2  );
        push( @result, @station_result );
      }
    }

    $yyyymmdd1 = increment_date_days( $yyyymmdd2, 1 );
    $days_remaining -= $query_days;
  }

  my @sorted_result = sort @result;
  return @sorted_result;
}



# query external web service.
# my @station_result =
#  query_station( $longitude, $latitude, $station, $parameter,
#                 $yyyymmdd1, $yyyymmdd2 );

sub query_station {
  my ($longitude,$latitude,$station,$parameter,$yyyymmdd1,$yyyymmdd2) = @_;

  my $command =
    $wget_command .
    "&begin_date=$yyyymmdd1&end_date=$yyyymmdd2" .
    "&datum=$parameter&station=$station'";

  my $result = '';
  my $the_pipe = 0;

  # Untaint command (match expression is arbitrary as far as -T is concerned):

  if ( $command =~ m#^(/[\w-]+/[\w -/.,':?=&\+]+)$# ) {
    $command = $1; # Re-assign first match, which is enough to satisfy -T.
    debug( "$0 executing command = $command" );
    %ENV = (); # Unset all environment variables prior to popen.
    # Complicated, supposedly safer way to open a readable pipe to command:
    my $pid = open( the_pipe, "-|" );

    if ( ! defined( $pid ) ) {
      die "Couldn't open pipe to subprocess";
    } elsif ( $pid ) { # Parent process.
      ++$remote_queries;
      debug( "reading result rows..." );

      # Read and one-line header that looks like this:
      # Date Time, Water Level, Sigma, O, F, R, L, Quality 

      my $header = <the_pipe>;

      if ( defined( $header ) ) {
        debug( "header = $header" );

        while ( my $line = <the_pipe> ) {
          debug( "line = $line" );

          # Read and process resulting lines that looks like this:
          # 2006-07-18 00:00,-1.145,0.004,0,0,0,0,v
          # 2006-07-18 00:06,-1.136,0.003,0,0,0,0,v

          my @data_row = split( /,/, $line );
          my $words = @data_row;

          if ( $words >= 2 ) {
            my $yyyy_mm_dd_hh_mm = $data_row[ 0 ];
            my $value            = $data_row[ 1 ];
            my $yyyy     = substr( $yyyy_mm_dd_hh_mm, 0, 4 );
            my $mm       = substr( $yyyy_mm_dd_hh_mm, 5, 2 );
            my $dd       = substr( $yyyy_mm_dd_hh_mm, 8, 2 );
            my $yyyymmdd = $yyyy . $mm . $dd;
            my $hh       = substr( $yyyy_mm_dd_hh_mm, 11, 2 );
            debug( "$yyyymmdd $hh $value" );

            if ( in_range( $yyyymmdd, $yyyymmdd1, $yyyymmdd2 ) &&
                 in_range( $hh, 0, 23 ) && "$value" ne '' ) {
              my $timestamp =
                substr( $yyyymmdd, 0, 4 ) . '-' .
                substr( $yyyymmdd, 4, 2 ) . '-' .
                substr( $yyyymmdd, 6, 2 ) . 't' .
                $hh . ':00:00z';

              if ( is_valid_time( $timestamp ) ) {
                $timestamp =~ tr /t/T/;
                $timestamp =~ s /z/-0000/;
                $result .=
                  "$timestamp\t$longitude\t$latitude\t$station\t$value\n";                
              }
            }
          }
        }
      }

      close( the_pipe ) or die $!;
    } else { # Child process.
      exec( $command ) or die "can't exec program: $!";
    }
  } else {
    print STDERR "\n$0: command contains invalid characters.\n";
  }

  my @sorted_result = sort split( /\n/, $result );
  return @sorted_result;
}



############################### HELPER ROUTINES ##############################



# debug( message );

sub debug {
  my $message = shift;

  if ( $debugging ) {
    print STDERR "\n$message\n";
####print STDOUT "\n$message\n";
  }
}



# my $result = parse_option( $option, $value, $option_name, $valid_values );
# my $result = parse_option( $variable, $value, 'COVERAGE', 'ozone pm25' );

sub parse_option {
  my ( $option, $value, $option_name, $valid_values ) = @_;
  my $result = 0;

  if ( $option ne '' ) {
    print STDERR "\nRedundant $option_name option: '$value'\n";
  } else {
    $result = index( " $valid_values ", " $value " ) != -1;

    if ( $result ) {
      $_[ 0 ] = $value;
    } else {
      print STDERR "\nInvalid $option_name option: '$value'\n";
    }
  }

  return $result;
}



# my $ok = in_range( $value, $minimum, $maximum );

sub in_range {
  my ( $value, $minimum, $maximum ) = @_;
  my $result = $value >= $minimum && $value <= $maximum;
  return $result;
}



# my $is_valid = is_valid_time( '2001-08-26t20:00:00z/2001-08-27t23:59:59z' );

sub is_valid_time {
  my $time = shift;
  my $result = 0;
  my $length = length( $time );

  if ( $length == 41 ) {
    $result = is_valid_time( substr( $time, 0, 20 ) );
    $result = $result && substr( $time, 20, 1 ) eq '/';
    $result = $result && is_valid_time( substr( $time, 21, 20 ) );
    $result = $result && substr( $time, 0, 20 ) le substr( $time, 21, 20 );
  } elsif ( $length == 20 ) {
    my $year   = substr( $time, 0, 4 );
    my $month  = substr( $time, 5, 2 );
    my $day    = substr( $time, 8, 2 );
    my $hour   = substr( $time, 11, 2 );
    my $minute = substr( $time, 14, 2 );
    my $second = substr( $time, 17, 2 );
    $result = in_range( $year, 1900, 3000 );
    $result = $result && in_range( $month, 1, 12 );
    $result = $result && in_range( $day, 1, days_in_month( $year, $month ) );
    $result = $result && in_range( $hour, 0, 23 );
    $result = $result && in_range( $minute, 0, 59 );
    $result = $result && in_range( $second, 0, 59 );
    $result = $result && substr( $time, 4, 1 ) eq '-';
    $result = $result && substr( $time, 7, 1 ) eq '-';
    $result = $result && substr( $time, 10, 1 ) eq 't';
    $result = $result && substr( $time, 13, 1 ) eq ':';
    $result = $result && substr( $time, 16, 1 ) eq ':';
    $result = $result && substr( $time, 19, 1 ) eq 'z';
  }

  return $result;
}



# increment_timestamp( $yyyy, $mm, $dd, $hh );

sub increment_timestamp {
  my ( $yyyy, $mm, $dd, $hh ) = @_;
  my $hours_per_day   = 23;
  my $months_per_year = 12;
  my $days_this_month = days_in_month( $yyyy, $mm );
  ++$hh;

  if ( $hh > $hours_per_day ) {
    $hh = 0;
    ++$dd;

    if ( $dd > $days_this_month ) {
      $dd = 1;
      ++$mm;

      if ( $mm > $months_per_year ) {
        $mm = 1;
        ++$yyyy;
      }
    }
  }

  $_[ 0 ] = $yyyy;
  $_[ 1 ] = $mm;
  $_[ 2 ] = $dd;
  $_[ 3 ] = $hh;
}



# my $yyyymmddhh = integer_timestamp( $yyyy, $mm, $dd, $hh );

sub integer_timestamp {
  my ( $yyyy, $mm, $dd, $hh ) = @_;
  my $result = int( $yyyy * 1000000 + $mm * 10000 + $dd * 100 + $hh );
  return $result;
}



# my $leap = is_leap_year( $year );

sub is_leap_year {
  my $year = shift;
  my $result = $year % 4 == 0 && ( $year % 100 != 0 || $year % 400 == 0 );
  return $result;
}



# my $days = days_in_month( $year, $month );

sub days_in_month {
  my $year  = shift;
  my $month = shift;

  # 30 days hath September, April, June and November...

  my @days_per_month = ( 31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31 );
  my $result = $days_per_month[ $month - 1 ];

  if ( $month == 2 ) {

    if ( is_leap_year( $year ) ) {
      ++$result;
    }
  }

  return $result;
}



# decrement_date( $yyyy, $mm, $dd );

sub decrement_date {
  my ( $yyyy, $mm, $dd ) = @_;
  --$dd;

  if ( $dd < 1 ) {
    --$mm;

    if ( $mm < 1 ) {
       --$yyyy;
       $mm = 12;
    }

    $dd = days_in_month( $yyyy, $mm );
  }

  $_[ 0 ] = $yyyy;
  $_[ 1 ] = $mm;
  $_[ 2 ] = $dd;
}



# increment_date( $yyyy, $mm, $dd );

sub increment_date {
  my ( $yyyy, $mm, $dd ) = @_;
  ++$dd;

  if ( $dd > 28 ) {
    my $days_this_month = days_in_month( $yyyy, $mm );

    if ( $dd > $days_this_month ) {
      $dd = 1;
      ++$mm;

      if ( $mm > 12 ) {
        $mm = 1;
        ++$yyyy;
      }
    }
  }

  $_[ 0 ] = $yyyy;
  $_[ 1 ] = $mm;
  $_[ 2 ] = $dd;
}



# increment_date_days( $yyyymmdd, $days );

sub increment_date_days {
  my ( $yyyymmdd, $days ) = @_;
  my $yyyy = int( $yyyymmdd / 10000 );
  my $mm   = int( $yyyymmdd / 100 ) % 100;
  my $dd   = $yyyymmdd % 100;

  while ( $days-- ) {
    increment_date( $yyyy, $mm, $dd );
  }

  my $result = $yyyy * 10000 + $mm * 100 + $dd;
  return $result;
}



